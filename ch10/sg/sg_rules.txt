// These are the esg (emergent stochastic / sentence generator) rules
// See emer/emergent/esg for details on syntax etc.

Sentence {
	Ate
	Drank
	Stirred
	Spread
	Kissed
	Gave
	Hit
	ThrewTossed
	ThrewHosted
	DroveTrans
	DroveMotiv
	ShedV
	RoseV
}

// note: not possible to do passive within these rules as the :P clause depends on :A
// do that as a post-processing step -- this is generating "internal" event rep

// Std agent used for most actions
Std:A {
	Busdriver:A =Agent
	Teacher:A   =Agent
	PitcherPers:A =Agent
	Schoolgirl:A =Agent
}

Std:Ao ? {
	=Mod=CoAgent
	Busdriver:A { 'with' Teacher:A =CoAgent=Teacher }
	Teacher:A { 'with' Busdriver:A =CoAgent=Busdriver }
	Schoolgirl:A { 'with' PitcherPers:A =CoAgent=PitcherPers }
	PitcherPers:A { 'with' Schoolgirl:A =CoAgent=Schoolgirl }
}

// Note: following are re-used across Agent and CoAgent so no state there

Busdriver:A {
	%70 'busdriver'
	%20 'adult'
	%10 'someone'
}

Teacher:A {
	%70 'teacher'
	%20 'adult'
	%10 'someone'
}

PitcherPers:A {
	%70 'pitcherpers'
	%20 'child'
	%10 'someone'
}

Schoolgirl:A {
	%70 'schoolgirl'
	%20 'child'
	%10 'someone'
}

Busdriver:P {
	%70 'busdriver'
	%20 'adult'
	%10 'someone'
}

Teacher:P {
	%70 'teacher'
	%20 'adult'
	%10 'someone'
}

PitcherPers:P {
	%70 'pitcherpers'
	%20 'child'
	%10 'someone'
}

Schoolgirl:P {
	%70 'schoolgirl'
	%20 'child'
	%10 'someone'
}

Spoon:Pi {
	%70 'with' 'spoon'
	%20 'with' 'utensil'
	%10 'with' 'something'
}

Knife:Pi {
	%70 'with' 'knife'
	%20 'with' 'utensil'
	%10 'with' 'something'
}

Icedtea:P {
	=Patient
	%80 'icedtea'
	%20 'something'
}

Koolaid:P {
	=Patient
	%80 'koolaid'
	%20 'something'
}

ShedN:P {
	%80 'shedn'
	%20 'something'
}

BallSphere:P {
	%80 'ballsphere'
	%20 'something'
}

BatAnim:P {
	%80 'batanim'
	%20 'something'
}

PitcherCont:P {
	%80 'pitchercont'
	%20 'something'
}

Bus:P {
	%80 'bus'
	%20 'something'
}

Spot:P {
	%80 'spot'
	%20 'dog'
}

Spot:A {
	%80 'spot'
	%20 'dog'
}

Fur:P {
	%80 'fur'
	%20 'something'
}

BatBall:P {
	%80 'batball'
	%20 'something'
}

Spoon:P {
	%80 'spoon'
	%20 'something'
}

RoseN:P {
	%80 'rosen'
	%20 'something'
}

Icecream:GP {
	%80 'icecream'
	%20 'something'
}

BallParty:P {
	%80 'ballparty'
	%20 'something'
}

////////////////////////////////////////////////////
//  Ate

Ate {
	=Action
	Std:A Ate:V Ate:P Ate:M
}

Ate:V {
	%80 'ate'
	%20 'consumed'
}

Ate:P ? {
	Busdriver:A {
		%70 Steak:P
		%10 Soup:P
		%10 Icecream:P
		%10 Crackers:P
	}
	Teacher:A {
		%70 Soup:P
		%10 Steak:P
		%10 Icecream:P
		%10 Crackers:P
	}
	PitcherPers:A {
		%25 Soup:P
		%25 Steak:P
		%25 Icecream:P
		%25 Crackers:P
	}
	Schoolgirl:A {
		%25 Soup:P
		%25 Steak:P
		%25 Icecream:P
		%25 Crackers:P
	}
}

Steak:P {
	=Patient
	%70 'steak'
	%20 'food'
	%10 'something'
}

Soup:P {
	=Patient
	%70 'soup'
	%20 'food'
	%10 'something'
}

Icecream:P {
	=Patient
	%70 'icecream'
	%20 'food'
	%10 'something'
}

Crackers:P {
	=Patient
	%70 'crackers'
	%20 'food'
	%10 'something'
}

Ate:M {
	Ate:Pi
	Ate:AoPc
	Ate:L
	Ate:R
}

Ate:Pi ? {
	=Mod=Instrument
	Steak:P {
		=Instrument=Knife
		Knife:Pi
	}
	Soup:P || Icecream:P {
		=Instrument=Spoon
		Spoon:Pi
	}
	Crackers:P {
		=Instrument=Finger
		%80 'with' 'finger'
		%20 'with' 'something'
	}
}

Ate:AoPc ? {
	Steak:P || Icecream:P { Std:Ao }
	Soup:P  || Crackers:P { Ate:Pc }
}

Ate:Pc ? {
	=Mod=CoPatient
	Soup:P {
		=CoPatient=Crackers
		%80 'with' 'crackers'
		%20 'with' 'something'
	}
	Crackers:P {
		=CoPatient=Jelly
		%80 'with' 'jelly'
		%20 'with' 'something'
	}
}

Ate:L ? {
	=Mod=Location
	Steak:P || Soup:P || Crackers:P {	'in' 'kitchen' =Location=Kitchen }
	Icecream:P {	'in' 'park' =Location=Park }
}

Ate:R ? {
	=Mod=Adverb
	(Busdriver:A || PitcherPers:A) && (Steak:P || Soup:P || Crackers:P) { 'with' 'gusto' =Adverb=Gusto }
	(Teacher:A || Schoolgirl:A) && (Steak:P || Soup:P || Crackers:P) { 'with' 'daintiness' =Adverb=Daintiness }
	Icecream:P {	'with' 'pleasure' =Adverb=Pleasure }
}

////////////////////////////////////////////////////
//  Drank

Drank {
	=Action
	Std:A Drank:V Beverage:P Drank:M
}

Drank:V {
	%80 'drank'
	%20 'consumed'
}

Beverage:P ? {
	Busdriver:A {
		%80 Icedtea:P
		%20 Koolaid:P
	}
	Teacher:A {
		%80 Icedtea:P
		%20 Koolaid:P
	}
	PitcherPers:A {
		%20 Icedtea:P
		%80 Koolaid:P
	}
	Schoolgirl:A {
		%20 Icedtea:P
		%80 Koolaid:P
	}
}

Drank:M {
	Std:Ao
	Drank:L
	Drank:R
}

Drank:L ? {
	=Mod=Location
	Icedtea:P {	'in' 'livingroom' =Location=Livingroom }
	Koolaid:P {	'in' 'kitchen' =Location=Kitchen }
}

Drank:R ? {
	=Mod=Adverb
	Busdriver:A || PitcherPers:A { 'with' 'gusto' =Adverb=Gusto }
	Teacher:A || Schoolgirl:A { 'with' 'daintiness' =Adverb=Daintiness }
}

////////////////////////////////////////////////////
//  Stirred

Stirred {
	=Action
	Std:A 'stirred' Beverage:P Stirred:M
}

Stirred:M {
	Stirred:L 
	Stirred:Pi
}

Stirred:L {
	=Mod=Location
	'in' 'pitchercont' =Location=PitcherCont
}

Stirred:Pi {
	=Mod=Instrument		
	Spoon:Pi =Instrument=Spoon
}

////////////////////////////////////////////////////
//  Spread

Spread {
	=Action
	Std:A 'spread' Spread:P Spread:M
}

Spread:P {
	=Patient=Jelly
	'jelly'
}

Spread:M {
	Spread:L
	Spread:Pi
}

Spread:L {	
	=Mod=Location
	'in' 'kitchen' =Location=Kitchen
}

Spread:Pi {	
	 =Mod=Instrument	
	Knife:Pi =Instrument=Knife
}

////////////////////////////////////////////////////
//  Kissed

Kissed {
	=Action
	Std:A 'kissed' Kissed:P Kissed:M
}

Kissed:P ? {
	Busdriver:A {
		Teacher:P =Patient
		Schoolgirl:P =Patient
	}
	Teacher:A {
		Busdriver:P =Patient
		PitcherPers:P =Patient
	}
	PitcherPers:A {
		Teacher:P =Patient
		Schoolgirl:P =Patient
	}
	Schoolgirl:A {
		Busdriver:P =Patient
		PitcherPers:P =Patient
	}
}

Kissed:M {
	Kissed:L
}

Kissed:L ? {
	=Mod=Location
	Busdriver:A || Teacher:A { 'in' 'park' =Location=Park }
	(PitcherPers:A && Teacher:P) || (Schoolgirl:A && Busdriver:P) { 'in' 'park' =Location=Park }
	(PitcherPers:A && Schoolgirl:P) || (Schoolgirl:A && PitcherPers:P) { 'in' 'shedn' =Location=ShedN }
}

////////////////////////////////////////////////////
//  Gave

Gave {
	=Action
	Std:A 'gave' Gave:P Gave:M
}

Gave:P {
	RoseN:P =Patient
	Icecream:GP =Patient
}

Gave:M {
	Gave:Pr
}

Gave:Pr ? {
	=Mod=Recipient
	Busdriver:A && RoseN:P { 'to' Teacher:P =Recipient=Teacher }
	Busdriver:A && Icecream:GP { 'to' Schoolgirl:P =Recipient=Schoolgirl }
	Teacher:A && RoseN:P { 'to' Busdriver:P =Recipient=Busdriver }
	Teacher:A && Icecream:GP { 'to' PitcherPers:P =Recipient=PitcherPers }
	PitcherPers:A && RoseN:P { 'to' Teacher:P =Recipient=Teacher }
	PitcherPers:A && Icecream:GP { 'to' Schoolgirl:P =Recipient=Schoolgirl }
	Schoolgirl:A && RoseN:P { 'to' Busdriver:P =Recipient=Busdriver }
	Schoolgirl:A && Icecream:GP { 'to' PitcherPers:P =Recipient=PitcherPers }
}

////////////////////////////////////////////////////
//  Hit

Hit {
	=Action
	Std:A 'hit' Hit:P Hit:M
}

Hit:P ? {
	Busdriver:A {
		%25 ShedN:P =Patient
		%40 BallSphere:P =Patient
		%10 BatAnim:P =Patient
		%25 PitcherPers:P =Patient
	}
	Teacher:A {
		PitcherCont:P =Patient
		PitcherPers:P =Patient
		Schoolgirl:P =Patient
	}
	PitcherPers:A {
		%40 BallSphere:P =Patient
		%10 BatAnim:P =Patient
		%25 Bus:P =Patient
		%25 Busdriver:P =Patient
	}
	Schoolgirl:A {
		PitcherCont:P =Patient
		Spot:P =Patient
		Teacher:P =Patient
	}
}

Hit:M {
	Hit:Pi
	Hit:L
}

Hit:Pi ? {
	=Mod=Instrument
	ShedN:P { 'with' Bus:P =Instrument=Bus }
	BallSphere:P { 'with' BatBall:P =Instrument=BatBall }
	BatAnim:P { 'with' BatBall:P =Instrument=BatBall }
	Busdriver:A && PitcherPers:P { 'with' BallSphere:P =Instrument=BallSphere }
	PitcherCont:P { 'with' Spoon:P =Instrument=Spoon }
	Teacher:A && PitcherPers:P { 'with' PitcherCont:P =Instrument=PitcherCont }
	Schoolgirl:P { 'with' Spoon:P =Instrument=Spoon }
	Bus:P { 'with' BallSphere:P =Instrument=BallSphere }
	PitcherPers:A && Busdriver:P { 'with' BallSphere:P =Instrument=Busdriver }
	Spot:P { 'with' Spoon:P =Instrument=Spoon }
	Teacher:P { 'with' BallSphere:P =Instrument=BallSphere }
}

Hit:L ? {
	=Mod=Location
	ShedN:P { 'in' Bus:P =Location=Bus }
	BallSphere:P { 'in' 'park' =Location=Park }
	BatAnim:P { 'in' 'shedn' =Location=ShedN }
	Busdriver:A && PitcherPers:P { 'in' 'park' =Location=Park }
	PitcherCont:P { 'in' 'livingroom' =Location=Livingroom }
	Teacher:A && PitcherPers:P { 'in' 'livingroom' =Location=Livingroom }
	Schoolgirl:P { 'in' 'kitchen' =Location=Kitchen }
	Bus:P { 'in' 'park' =Location=Park }
	PitcherPers:A && Busdriver:P { 'in' 'park' =Location=Park }
	Spot:P { 'in' 'kitchen' =Location=Kitchen }
	Teacher:P { 'in' 'kitchen' =Location=Kitchen }
}

////////////////////////////////////////////////////
//  ThrewTossed

ThrewTossed {
	=Action
	ThrewTossed:A 'threwtossed' ThrewTossed:P ThrewTossed:M
}

ThrewTossed:A {
	Busdriver:A =Agent
	PitcherPers:A =Agent
}

ThrewTossed:P {
	 BallSphere:P =Patient
}

ThrewTossed:M {
	ThrewTossed:Pr
	ThrewTossed:L
}

ThrewTossed:Pr ? {
	=Mod=Recipient
	Busdriver:A { 'to' PitcherPers:P =Recipient=PitcherPers }
	PitcherPers:A { 'to' Busdriver:P =Recipient=Busdriver }
}

ThrewTossed:L {
	=Mod=Location
	'in' 'park' =Location=Park
}

////////////////////////////////////////////////////
//  ThrewHosted

ThrewHosted {
	=Action
	ThrewHosted:A 'threwhosted' ThrewHosted:P ThrewHosted:M
}

ThrewHosted:A {
	Teacher:A   =Agent
	Schoolgirl:A =Agent
}

ThrewHosted:P {
	BallParty:P =Patient
}

ThrewHosted:M {
	ThrewHosted:Ao
	ThrewHosted:L
}

ThrewHosted:Ao ? {
	=Mod=CoAgent
	Teacher:A { 'with' Schoolgirl:P =CoAgent=Schoolgirl }
	Schoolgirl:A { 'with' Teacher:P =CoAgent=Teacher }
}

ThrewHosted:L {
	=Mod=Location
	'in' 'livingroom' =Location=Livingroom
}

////////////////////////////////////////////////////
//  DroveTrans

DroveTrans {
	=Action
	DroveTrans:A 'drovetrans' DroveTrans:P DroveTrans:M
}

DroveTrans:A {
	Busdriver:A =Agent
}

DroveTrans:P {
	 Bus:P =Patient
	 PitcherPers:P =Patient
	 Schoolgirl:P =Patient
}

DroveTrans:M {
	DroveTrans:R
	DroveTrans:L
}

DroveTrans:R {
	=Mod=Adverb
	'with' 'gusto' =Adverb=Gusto
}

DroveTrans:L ? {
	=Mod=Location
	Bus:P { 'to' 'park' =Location=Park }
	PitcherPers:P || Schoolgirl:P { 'in' 'bus' =Location=Bus }
}

////////////////////////////////////////////////////
//  DroveMotiv

DroveMotiv {
	=Action
	DroveMotiv:A 'drovemotiv' DroveMotiv:P DroveMotiv:M
}

DroveMotiv:A {
	Teacher:A =Agent
}

DroveMotiv:P {
	 PitcherPers:P =Patient
	 Schoolgirl:P =Patient
}

DroveMotiv:M {
	DroveMotiv:R
}

DroveMotiv:R {
	=Mod=Adverb
	'with' 'gusto' =Adverb=Gusto
}

////////////////////////////////////////////////////
//  ShedV

ShedV {
	=Action
	ShedV:A 'shedv' ShedV:P ShedV:M
}

ShedV:A {
	Spot:A =Agent
}

ShedV:P {
	 Fur:P =Patient
}

ShedV:M {
	ShedV:L
}

ShedV:L {
	=Mod=Location
	'in' 'livingroom' =Location=Livingroom
}

////////////////////////////////////////////////////
//  RoseV

RoseV {
	=Action
	Std:A 'rosev' RoseV:M =Patient=None
}

RoseV:M {
	RoseV:L
	Std:Ao
}

RoseV:L {
	=Mod=Location
	'in' 'livingroom' =Location=Livingroom
}

